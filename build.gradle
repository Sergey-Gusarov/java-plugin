buildscript {
    repositories {
        jcenter()
        dependencies {
            classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.50'
            classpath 'ru.yoomoney.gradle.plugins:artifact-release-plugin:3.7.0'
        }
    }
}

plugins {
    id "com.jfrog.bintray" version "1.8.4"
    id "com.gradle.plugin-publish" version "0.12.0"
    id "java-gradle-plugin"
    id "ru.yoomoney.gradle.plugins.artifact-release-plugin" version "3.7.0"
}

releaseSettings {
    releaseTasks = ["build", "bintrayUpload", "publishPlugins"]
    pathToGitPrivateSshKey = System.getenv("GIT_PRIVATE_SSH_KEY_PATH")
    passphraseToGitPrivateSshKey = System.getenv("GIT_KEY_PASSPHRASE")
    gitUsername = "yoomoney-robot"
    gitEmail = "SvcReleaserBackend@yoomoney.ru"
    changelogRequired = true
    addPullRequestLinkToChangelog = true
    pullRequestInfoProvider = "GitHub"
    githubAccessToken = System.getenv("GITHUB_TOKEN")
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/yoomoney-gradle-plugins/maven/" }
    maven { url "https://plugins.gradle.org/m2/" }
}

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'groovy'
apply from: 'dependencies.gradle'

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileJava {
    options.compilerArgs.addAll(['--release', '8'])
}

jar {
    manifest {
        attributes(
                'Implementation-Version': "$project.version",
                'Implementation-Title': "$project.name",
                'Built-By': java.lang.System.getProperty('user.name'),
                'Built-Date': new Date().format('yyyy-MM-dd'),
                'Copyright': '2021 NBCO YooMoney LLC',
                'License': 'MIT'
        )
    }

    from(projectDir.absolutePath) {
        include "CHANGELOG.md", "README.md"
        into "META-INF"
    }
}

apply plugin: 'idea'
idea {
    project {
        jdkName = '1.8'
        languageLevel = '1.8'
        vcs = 'Git'
    }
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}
test {
    systemProperty "file.encoding", "UTF-8"
    testLogging {
        events "skipped", "failed"
        exceptionFormat "full"
        showExceptions true
        showCauses true
        showStackTraces true
    }
}

apply plugin: "jacoco"
jacocoTestReport {
    reports {
        html.enabled = true
        xml.enabled = true
    }
}
check.dependsOn jacocoTestReport

javadoc {
    options {
        docEncoding = 'UTF-8'
        charSet = 'UTF-8'
        encoding = 'UTF-8'
        failOnError = true
    }
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

group = 'ru.yoomoney.gradle.plugins'

gradlePlugin {
    plugins {
        javaPlugin {
            id = 'ru.yoomoney.gradle.plugins.java-plugin'
            implementationClass = 'ru.yoomoney.gradle.plugins.backend.build.JavaPlugin'
            displayName = 'Plugin for java project'
        }
        kotlinPlugin {
            id = 'ru.yoomoney.gradle.plugins.kotlin-plugin'
            implementationClass = 'ru.yoomoney.gradle.plugins.backend.kotlin.KotlinModulePlugin'
            displayName = 'Plugin for kotlin project'
        }
    }
}

pluginBundle {
    website = 'https://github.com/yoomoney-gradle-plugins/java-plugin'
    vcsUrl = 'https://github.com/yoomoney-gradle-plugins/java-plugin.git'
    description = 'Plugin for java and kotlin project'
    tags = ['java', 'kotlin', 'gradle', 'plugin']
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId group
            artifactId 'java-plugin'
            version project.version
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']

    dryRun = false
    publish = true
    pkg {
        name = 'java-plugin'
        repo = 'maven'
        userOrg = 'yoomoney-gradle-plugins'
        desc = 'Plugin for java and kotlin project'
        websiteUrl = 'https://github.com/yoomoney-gradle-plugins/java-plugin'
        issueTrackerUrl = 'https://github.com/yoomoney-gradle-plugins/java-plugin/issues'
        vcsUrl = 'https://github.com/yoomoney-gradle-plugins/java-plugin.git'
        licenses = ['MIT']
        labels = ['java', 'kotlin', 'gradle', 'plugin']
        publicDownloadNumbers = true
        githubRepo = 'yoomoney-gradle-plugins/java-plugin'
        githubReleaseNotesFile = 'CHANGELOG.md'
        version {
            gpg {
                sign = true
                passphrase = System.getenv('BINTRAY_PASSPHRASE')
            }
            name = project.version
            released = new Date()
            vcsTag = project.version
        }
    }
}